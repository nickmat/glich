// gw-001.glcs  Create gw scheme format table.

let sch = "gw";

let d1 = 2459984;

let so = @sch:object.(sch);
let sfl = so[formats];
let size = @size( sfl );

write nl;
write "<table class='basic data'>" nl;
write "<tr><th colspan='6'>Formats - Grammar " + so[grammar] + "</th></tr>" nl;
write "<tr><th>Code</th><th>Rule</th><th>Pseudo:in</th><th>Pseudo:out</th><th>Example</th><th>Note</th></tr>" nl;

let index = 0;
do {
    let fmt = sfl[(index)];
    let sig = sch + ":" + fmt;
    let fo = @fmt:object.(sig);
    let note = @if(fo[visible], "", "H");
    if so[default:in] = fmt or so[default:out] = fmt {
        if @size( note ) > 0 {
            note += ", ";
        };
        note += "D";
    };
    if fo[shorthand] {
        if @size( note ) > 0 {
            note += ", ";
        };
        note += "S";
    };
        
    let row = "<tr><td><b>" + fo[code] + "</b></td>"
      + "<td>" +fo[rules] + "</td>"
      + "<td>" + fo[pseudo:in] + "</td>"
      + "<td>" + fo[pseudo:out] + "</td>"
      + "<td class='example'>" + @text.(sig)(d1) + "</td>"
      + "<td>" + note + "</td></tr>";

    write row nl;

    index += 1;
    until index = size;
};

write "<tr><td colspan='6'>Notes: D = Default, H = Hidden, S = Range shortcut supported." nl;
write "Example based on jdn# " + d1 + "</td></tr>" nl;
write "</table>" nl;


/*[OUTPUT]

<table class='basic data'>
<tr><th colspan='6'>Formats - Grammar gw</th></tr>
<tr><th>Code</th><th>Rule</th><th>Pseudo:in</th><th>Pseudo:out</th><th>Example</th><th>Note</th></tr>
<tr><td><b>ywd</b></td><td>text</td><td>year week day</td><td>yyyy ww Wday</td><td class='example'>2023 6 Wed</td><td>D, S</td></tr>
<tr><td><b>ywd+</b></td><td>text</td><td>year week day</td><td>yyyy ww Weekday</td><td class='example'>2023 6 Wednesday</td><td>S</td></tr>
<tr><td><b>iso</b></td><td>iso8601</td><td>ISO:8601 Week -YYYY</td><td>-YYYY-Www-D</td><td class='example'>2023-W06-3</td><td>S</td></tr>
<tr><td><b>def</b></td><td>text</td><td>year week day</td><td>yyyy ww d</td><td class='example'>2023 6 3</td><td>H, S</td></tr>
<tr><td><b>u</b></td><td>unit</td><td>9y 9w 9d</td><td>9y 9w 9d</td><td class='example'>2023y 6w 3d</td><td>H</td></tr>
<tr><td colspan='6'>Notes: D = Default, H = Hidden, S = Range shortcut supported.
Example based on jdn# 2459984</td></tr>
</table>

[OUTPUT]*/
