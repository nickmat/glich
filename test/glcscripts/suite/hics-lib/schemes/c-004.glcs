// c-004.glcs  Create c scheme format table.

let sch = "c";

let d1 = 2460866;
let d2 = 2460896;
let d3 = 2460925;

let so = @sch:object.(sch);
let sfl = so[.formats];
let size = @size( sfl );
let index = 0;

write nl;
write "<table class='basic data'>" nl;
write "<tr><th colspan='4'>Formats - Grammar " + so[.grammar] + "</th></tr>" nl;
write "<tr><th>Code</th><th>Rule</th><th>Pseudo / Example</th><th>Note</th></tr>" nl;

do {
    let fmt = sfl[(index)];
    let sig = sch + ":" + fmt;
    let fo = @fmt:object.(sig);
    let note = @if(fo[.visible], "", "H");
    if so[.default:in] = fmt or so[.default:out] = fmt {
        if @size( note ) > 0 {
            note += ", ";
        };
        note += "D";
    };
    if fo[.shorthand] {
        if @size( note ) > 0 {
            note += ", ";
        };
        note += "S";
    };
    
    let p = fo[.pseudo:in];
    if @size(p) > 0 {
        p = "<b>Input:</b> " + p + "<br />";
    };
    p = "<td>" + p + "<b>Output:</b> " + fo[.pseudo:out] + "</td>";

    let row = "<tr><td rowspan='2'><b>" + fo[.code] + "</b></td>"
      + "<td rowspan='2'>" +fo[.rules] + "</td>"
      + p
      + "<td rowspan='2'>" + note + "</td>"
      + "</tr>";

    write row nl;
  
    let row2 = "<tr><td class='example'>"
      + @text.(sig)(d1) + "<br />"
      + @text.(sig)(d2) + "<br />"
      + @text.(sig)(d3)
      + "</td></tr>";
    write row2 nl;


    index += 1;
    until index = size;
};

write "<tr><td colspan='6'>Notes: D = Default, H = Hidden." nl;
write "Example based on jdn# " + d1, d2 + " and " + d3 + "</td></tr>" nl;
write "</table>" nl;


/*[OUTPUT]

<table class='basic data'>
<tr><th colspan='4'>Formats - Grammar c</th></tr>
<tr><th>Code</th><th>Rule</th><th>Pseudo / Example</th><th>Note</th></tr>
<tr><td rowspan='2'><b>ymld</b></td><td rowspan='2'>text</td><td><b>Input:</b> year month lmonth day<br /><b>Output:</b> yy mm FS dd</td><td rowspan='2'>S</td></tr>
<tr><td class='example'>4662 6 F 15<br />4662 6 S 15<br />4662 7 F 15</td></tr>
<tr><td rowspan='2'><b>ylmd</b></td><td rowspan='2'>text</td><td><b>Input:</b> year lmonth month day<br /><b>Output:</b> yy LM mm dd</td><td rowspan='2'>S</td></tr>
<tr><td class='example'>4662 Month 6 15<br />4662 Leap 6 15<br />4662 Month 7 15</td></tr>
<tr><td rowspan='2'><b>cymld</b></td><td rowspan='2'>text</td><td><b>Input:</b> cycle cyear month lmonth day<br /><b>Output:</b> cc cy mm fs dd</td><td rowspan='2'>D, S</td></tr>
<tr><td class='example'>78 42 6 f 15<br />78 42 6 s 15<br />78 42 7 f 15</td></tr>
<tr><td rowspan='2'><b>cylmd</b></td><td rowspan='2'>text</td><td><b>Input:</b> cycle cyear clmonth month day<br /><b>Output:</b> cc cy LM mm dd</td><td rowspan='2'></td></tr>
<tr><td class='example'>78 42 6 15<br />78 42 Leap 6 15<br />78 42 7 15</td></tr>
<tr><td rowspan='2'><b>cylmd_s</b></td><td rowspan='2'>text</td><td><b>Input:</b> cycle stem branch clmonth month day<br /><b>Output:</b> cc Stem-branch, LM mm dd</td><td rowspan='2'></td></tr>
<tr><td class='example'>78 Yǐ-sì 6 15<br />78 Yǐ-sì, Leap 6 15<br />78 Yǐ-sì 7 15</td></tr>
<tr><td rowspan='2'><b>full</b></td><td rowspan='2'>text</td><td><b>Output:</b> Cycle: cc, Year: Stem-branch (cy), LeapMon: mm, Day: dd</td><td rowspan='2'></td></tr>
<tr><td class='example'>Cycle: 78, Year: Yǐ-sì (42), Month: 6, Day: 15<br />Cycle: 78, Year: Yǐ-sì (42), Leap-month: 6, Day: 15<br />Cycle: 78, Year: Yǐ-sì (42), Month: 7, Day: 15</td></tr>
<tr><td rowspan='2'><b>animal</b></td><td rowspan='2'>text</td><td><b>Output:</b> (Cycle cc) Year of the Animal cy LeapMon mm Day dd</td><td rowspan='2'></td></tr>
<tr><td class='example'>(Cycle 78) Year of the Snake 42 Month 6 Day 15<br />(Cycle 78) Year of the Snake 42 Leap-month 6 Day 15<br />(Cycle 78) Year of the Snake 42 Month 7 Day 15</td></tr>
<tr><td rowspan='2'><b>def</b></td><td rowspan='2'>text</td><td><b>Input:</b> year month lmonth day cycle cyear clmonth stem branch<br /><b>Output:</b> yy mm lm dd cc cy cm ss bb</td><td rowspan='2'>H</td></tr>
<tr><td class='example'>4662 6 0 15 78 42 2 6<br />4662 6 1 15 78 42 1 2 6<br />4662 7 0 15 78 42 2 6</td></tr>
<tr><td rowspan='2'><b>u</b></td><td rowspan='2'>unit</td><td><b>Input:</b> 9year 9m 9lm 9d 9c 9y 9cm 9s 9b<br /><b>Output:</b> 9year 9m 9lm 9d 9c 9y 9cm 9s 9b</td><td rowspan='2'>H</td></tr>
<tr><td class='example'>4662year 6m 0lm 15d 78c 42y 2s 6b<br />4662year 6m 1lm 15d 78c 42y 1cm 2s 6b<br />4662year 7m 0lm 15d 78c 42y 2s 6b</td></tr>
<tr><td colspan='6'>Notes: D = Default, H = Hidden.
Example based on jdn# 2460866, 2460896 and 2460925</td></tr>
</table>

[OUTPUT]*/
