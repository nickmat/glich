// g-002.glcs  Create g scheme format table.

let sch = "g";

let d1 = 2459984;

let so = @sch:object.(sch);
let sfl = so[.formats];
let size = @size( sfl );

write nl;
write "<table class='basic data'>" nl;
write "<tr><th colspan='6'>Formats - Grammar " + so[.grammar] + "</th></tr>" nl;
write "<tr><th>Code</th><th>Rule</th><th>Pseudo:in</th><th>Pseudo:out</th><th>Example</th><th>Note</th></tr>" nl;

let index = 0;
do {
    let fmt = sfl[(index)];
    let sig = sch + ":" + fmt;
    let fo = @fmt:object.(sig);
    let note = @if(fo[.visible], "", "H");
    if so[.default:in] = fmt or so[.default:out] = fmt {
        if @size( note ) > 0 {
            note += ", ";
        };
        note += "D";
    };
    if fo[.shorthand] {
        if @size( note ) > 0 {
            note += ", ";
        };
        note += "S";
    };
        
    let row = "<tr><td><b>" + fo[.code] + "</b></td>"
      + "<td>" +fo[.rules] + "</td>"
      + "<td>" + fo[.pseudo:in] + "</td>"
      + "<td>" + fo[.pseudo:out] + "</td>"
      + "<td class='example'>" + @text.(sig)(d1) + "</td>"
      + "<td>" + note + "</td></tr>";

    write row nl;

    index += 1;
    until index = size;
};

write "<tr><td colspan='6'>Notes: D = Default, H = Hidden, S = Range shortcut supported." nl;
write "Example based on jdn# " + d1 + "</td></tr>" nl;
write "</table>" nl;


/*[OUTPUT]

<table class='basic data'>
<tr><th colspan='6'>Formats - Grammar g</th></tr>
<tr><th>Code</th><th>Rule</th><th>Pseudo:in</th><th>Pseudo:out</th><th>Example</th><th>Note</th></tr>
<tr><td><b>iso</b></td><td>iso8601</td><td>ISO:8601 Date -YYYY</td><td>-YYYY-MM-DD</td><td class='example'>2023-02-08</td><td>S</td></tr>
<tr><td><b>iso_set</b></td><td>iso8601</td><td>ISO:8601 Date -YYYY</td><td>[-YYYY-MM-DD]</td><td class='example'>[2023-02-08]</td><td>S</td></tr>
<tr><td><b>dmy</b></td><td>text</td><td>day month year</td><td>dd Mon yyyy</td><td class='example'>8 Feb 2023</td><td>D, S</td></tr>
<tr><td><b>dmy+</b></td><td>text</td><td>day month year</td><td>dd Month yyyy</td><td class='example'>8 February 2023</td><td>S</td></tr>
<tr><td><b>wdmy</b></td><td>text</td><td>wday day month year</td><td>Wday dd Mon yyyy</td><td class='example'>Wed 8 Feb 2023</td><td></td></tr>
<tr><td><b>wdmy+</b></td><td>text</td><td>wday day month year</td><td>Weekday dd Month yyyy</td><td class='example'>Wednesday 8 February 2023</td><td></td></tr>
<tr><td><b>std</b></td><td>text</td><td>day month year</td><td>ddth Month yyyy</td><td class='example'>8th February 2023</td><td>S</td></tr>
<tr><td><b>full</b></td><td>text</td><td>wday day month year</td><td>Weekday ddth Month yyyy</td><td class='example'>Wednesday 8th February 2023</td><td></td></tr>
<tr><td><b>mdy</b></td><td>text</td><td>month day year</td><td>Mon dd, yyyy</td><td class='example'>Feb 8, 2023</td><td>S</td></tr>
<tr><td><b>mdy+</b></td><td>text</td><td>month day year</td><td>Month dd, yyyy</td><td class='example'>February 8, 2023</td><td>S</td></tr>
<tr><td><b>wmdy</b></td><td>text</td><td>wday month day year</td><td>Wday, Mon dd, yyyy</td><td class='example'>Wed, Feb 8, 2023</td><td></td></tr>
<tr><td><b>wmdy+</b></td><td>text</td><td>wday month day year</td><td>Weekday, Month dd, yyyy</td><td class='example'>Wednesday, February 8, 2023</td><td></td></tr>
<tr><td><b>ymd</b></td><td>text</td><td>year month day</td><td>yyyy:Mon:dd</td><td class='example'>2023:Feb:8</td><td>S</td></tr>
<tr><td><b>def</b></td><td>text</td><td>year month day</td><td>yyyy mm dd</td><td class='example'>2023 2 8</td><td>H, S</td></tr>
<tr><td><b>u</b></td><td>unit</td><td>9y 9m 9d</td><td>9y 9m 9d</td><td class='example'>2023y 2m 8d</td><td>H</td></tr>
<tr><td colspan='6'>Notes: D = Default, H = Hidden, S = Range shortcut supported.
Example based on jdn# 2459984</td></tr>
</table>

[OUTPUT]*/
