// h-002.glcs  Create h scheme format table.

let sch = "h";

let d1 = 2460380;
let d2 = 2460409;
let d3 = 2460764;

let so = @sch:object.(sch);
let sfl = so[formats];
let size = @size( sfl );
let index = 0;

write nl;
write "<table class='basic data'>" nl;
write "<tr><th colspan='5'>Formats - Grammar " + so[grammar] + "</th></tr>" nl;
write "<tr><th>Code</th><th>Rule</th><th>Pseudo</th><th>Example</th><th>Note</th></tr>" nl;

do {
    let fmt = sfl[(index)];
    let sig = sch + ":" + fmt;
    let fo = @fmt:object.(sig);
    let note = @if(fo[visible], "", "H");
    if so[default:in] = fmt or so[default:out] = fmt {
        if @size( note ) > 0 {
            note += ", ";
        };
        note += "D";
    };
    if fo[shorthand] {
        if @size( note ) > 0 {
            note += ", ";
        };
        note += "S";
    };
    
    let p = fo[pseudo:in];
    if @size(p) > 0 {
        p = "<b>Input:</b> " + p + "<br />";
    };
    p = "<td>" + p + "<b>Output:</b> " + fo[pseudo:out] + "</td>";

    let row = "<tr><td><b>" + fo[code] + "</b></td>"
      + "<td>" +fo[rules] + "</td>"
      + p
      + "<td class='example'>" + @text.(sig)(d1) + "<br />"
      + @text.(sig)(d2) + "<br />"
      + @text.(sig)(d3) + "</td>"
      + "<td>" + note + "</td></tr>";

    write row nl;

    index += 1;
    until index = size;
};

write "<tr><td colspan='6'>Notes: D = Default, H = Hidden." nl;
write "Example based on jdn# " + d1, d2 + " and " + d3 + "</td></tr>" nl;
write "</table>" nl;


/*[OUTPUT]

<table class='basic data'>
<tr><th colspan='5'>Formats - Grammar h</th></tr>
<tr><th>Code</th><th>Rule</th><th>Pseudo</th><th>Example</th><th>Note</th></tr>
<tr><td><b>dmy_a</b></td><td>text</td><td><b>Input:</b> day cmonth year<br /><b>Output:</b> dd Mon yyyy</td><td class='example'>30 Adf 5784<br />29 Ads 5784<br />29 Adr 5785</td><td>S</td></tr>
<tr><td><b>dmy</b></td><td>text</td><td><b>Input:</b> day cmonth year<br /><b>Output:</b> dd Month yyyy</td><td class='example'>30 Adar I 5784<br />29 Adar II 5784<br />29 Adar 5785</td><td>D, S</td></tr>
<tr><td><b>dmy-</b></td><td>text</td><td><b>Input:</b> day month year<br /><b>Output:</b> dd mm yyyy</td><td class='example'>30 12 5784<br />29 13 5784<br />29 12 5785</td><td>S</td></tr>
<tr><td><b>wdmy</b></td><td>text</td><td><b>Input:</b> wsday day cmonth year<br /><b>Output:</b> Weekday dd Month yyyy</td><td class='example'>Yom Rishon 30 Adar I 5784<br />Yom Sheni 29 Adar II 5784<br />Yom Shabbat 29 Adar 5785</td><td></td></tr>
<tr><td><b>ymd</b></td><td>text</td><td><b>Input:</b> year cmonth day<br /><b>Output:</b> yyyy Month dd</td><td class='example'>5784 Adar I 30<br />5784 Adar II 29<br />5785 Adar 29</td><td>S</td></tr>
<tr><td><b>ymd-</b></td><td>text</td><td><b>Input:</b> year month day<br /><b>Output:</b> yyyy mm dd</td><td class='example'>5784 12 30<br />5784 13 29<br />5785 12 29</td><td>S</td></tr>
<tr><td><b>gedcom</b></td><td>text</td><td><b>Input:</b> day month year<br /><b>Output:</b> dd MON yyyy</td><td class='example'>30 ADR 5784<br />29 ADS 5784<br />29 ADR 5785</td><td>S</td></tr>
<tr><td><b>def</b></td><td>text</td><td><b>Input:</b> year month day cmonth<br /><b>Output:</b> yyyy mm dd mm</td><td class='example'>5784 12 30 14<br />5784 13 29 13<br />5785 12 29 12</td><td>H</td></tr>
<tr><td><b>u</b></td><td>unit</td><td><b>Input:</b> 9y 9m 9d 9cm<br /><b>Output:</b> 9y 9m 9d 9cm</td><td class='example'>5784y 12m 30d 14cm<br />5784y 13m 29d 13cm<br />5785y 12m 29d 12cm</td><td>H</td></tr>
<tr><td colspan='6'>Notes: D = Default, H = Hidden.
Example based on jdn# 2460380, 2460409 and 2460764</td></tr>
</table>

[OUTPUT]*/
