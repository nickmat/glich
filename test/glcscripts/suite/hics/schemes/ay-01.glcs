// ay-01.glc // Test ay hybrid scheme

lexicon M {
    name "Month names";
    fieldname month;
    lang en;
    pseudo     Month,     Mon;
    tokens {
        1,  "January",   "Jan";
        2,  "February",  "Feb";
        3,  "March",     "Mar";
        4,  "April",     "Apr";
        5,  "May";
        6,  "June",      "Jun";
        7,  "July",      "Jul";
        8,  "August",    "Aug";
        9,  "September", "Sep";
        10, "October",   "Oct";
        11, "November",  "Nov";
        12, "December",  "Dec";
    }
}

lexicon JG {
    name "Scheme names";
    fieldname scheme;
    lang en;
    pseudo    Scheme, Sch;
    tokens {
        0, "Julian",     "j";
        1, "Gregorian",  "g";
    }
}

grammar JG {
    inherit j;
    fields scheme year month day;
    rank year month day;
    lexicons M JG;
    alias pseudo {
        s, scheme;
    }
    format  dmys    "{day} |{month:M:a} |{year} |{scheme:JG:a}";
    format "dmys+", "{day} |{month:M} |{year} |{scheme:JG}";
    format  dmy     "{day} |{month:M:a} |{year}";
    preferred dmys;
}

scheme AY {
    name "Astronomical Year";
    base hybrid {
        fields scheme year, month, day;
        scheme 0 { base julian; }
        change 2299161;  // g# 15 Oct 1582
        scheme 1 { base gregorian; }
    }
    grammar JG;
}

writeln "";

writeln record.AY 2299160 + ", " + record.AY 2299161;
writeln record.AY:dmys "4 Oct 1582 j" + ", " + record.AY:dmys "15 Oct 1582 g";
writeln record.AY:dmys "4 Oct 1582" + ", " + record.AY:dmys "15 Oct 1582";
write @text.AY:dmys( 2299160 ), @text.AY:dmys( 2299161 ) nl;
write @text.:dmys( {s:AY 0,1582,10,4} ), @text.:dmys( {s:AY 1,1582,10,15} ) nl;
write @text.AY:dmy( 2299160 ), @text.AY:dmy( 2299161 ) nl;
write @date.AY:dmys( "4 Oct 1582" ), @date.AY:dmys( "15 Oct 1582" ) nl;
write @date( {s:AY 0,1582,10,4} ), @date( {s:AY 1,1582,10,15} ) nl;

/*[OUTPUT]

{s:AY 0, 1582, 10, 4}, {s:AY 1, 1582, 10, 15}
{s:AY 0, 1582, 10, 4}, {s:AY 1, 1582, 10, 15}
{s:AY 0, 1582, 10, 4}, {s:AY 1, 1582, 10, 15}
4 Oct 1582 j, 15 Oct 1582 g
4 Oct 1582 j, 15 Oct 1582 g
4 Oct 1582, 15 Oct 1582
2299160, 2299161
2299160, 2299161

[OUTPUT]*/
