// ja-01.glcs  - Test version of Julian Annunciation using calculated fields.

lexicon W {
    name "Weekday names";
    fieldname wday;
    lang en;
    pseudo    Weekday,  WDay;
    tokens {
        1, "Monday",    "Mon";
        2, "Tuesday",   "Tue";
        3, "Wednesday", "Wed";
        4, "Thursday",  "Thur";
        5, "Friday",    "Fri";
        6, "Saturday",  "Sat";
        7, "Sunday",    "Sun";
    }
}

lexicon M {
    name "Month names";
    fieldname month;
    lang en;
    pseudo     Month,     Mon;
    tokens {
        1,  "January",   "Jan";
        2,  "February",  "Feb";
        3,  "March",     "Mar";
        4,  "April",     "Apr";
        5,  "May";
        6,  "June",      "Jun";
        7,  "July",      "Jul";
        8,  "August",    "Aug";
        9,  "September", "Sep";
        10, "October",   "Oct";
        11, "November",  "Nov";
        12, "December",  "Dec";
    }
}

grammar JA {
    fields year month day;
    optional wday;
    calculated cyear;
    rank cyear, month, day;
    lexicons M, W;
    alias pseudo { w wday; dd day; mm month; yyyy cyear; }
    alias unit { d day; m month; y year; }

    // Record: year month day cyear
    function calc_to_def {
        year = @if( month<3 or (month=3 and day<25), cyear+1, cyear);
        result = this;
    }
    function def_to_calc {
        cyear = @if( month<3 or (month=3 and day<25), year-1, year);
        result = this;
    }
    use { calculate def_to_calc; to_text def_to_calc; from_text calc_to_def; }

    format  dmc {
        inout "{day} |{month:M:a} |{cyear}";
        use:in calc_to_def;
    }
    format  wdmc {
        inout "{wday:W:a} |{day} |{month:M:a} |{cyear}";
        use:in calc_to_def;
    }
    format  out {
        output "{day::os} |{month:M} |{cyear}";
    }
    preferred dmc;
}

scheme JA {
    name "Julian Annunciation";
    base julian;
    grammar JA;
}


write nl;
write @record.JA( 2360328 ), @record.JA( 2360329 ) nl;
write @record.JA:dmc( "24 mar 1749" ), @record.JA:dmc( "25 Mar 1750" ) nl;
write @text.JA:wdmc( 2360328 ), @text.JA:wdmc( 2360329 ) nl;
write @text.JA:wdmc( {s:JA 1750,3,24} ), @text.JA:wdmc( {s:JA 1750,3,25} ) nl;
write @text.JA:wdmc ({s:JA ,3,24,1749}@calc_to_def), @text.JA:wdmc ({s:JA ,3,25,1750}@calc_to_def) nl;
write @date.JA:dmc( "24 mar 1749" ), @date.JA:dmc( "25 Mar 1750" ) nl;
write @date.JA:dmc( {s:JA 1750,3,24} ), @date.JA:dmc( {s:JA 1750,3,25} ) nl;
write @date.JA:dmc( {s:JA ,3,24,1749}@calc_to_def ), @date.JA:dmc( {s:JA ,3,25,1750}@calc_to_def ) nl;
write @text.JA:out( 2432814 ) nl;


/*[OUTPUT]

{s:JA 1750, 3, 24, 1749, 6}, {s:JA 1750, 3, 25, 1750, 7}
{s:JA 1750, 3, 24, 1749, 6}, {s:JA 1750, 3, 25, 1750, 7}
Sat 24 Mar 1749, Sun 25 Mar 1750
Sat 24 Mar 1749, Sun 25 Mar 1750
Sat 24 Mar 1749, Sun 25 Mar 1750
2360328, 2360329
2360328, 2360329
2360328, 2360329
6th September 1948

[OUTPUT]*/
