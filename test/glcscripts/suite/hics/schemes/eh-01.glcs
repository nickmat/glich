// eh-01.glcs  - Test version of Julian Spanish Era using scheme epoch sub-statement.

lexicon W {
    name "Weekday names";
    fieldname wday;
    lang en;
    pseudo    Weekday,  WDay;
    tokens {
        1, "Monday",    "Mon";
        2, "Tuesday",   "Tue";
        3, "Wednesday", "Wed";
        4, "Thursday",  "Thur";
        5, "Friday",    "Fri";
        6, "Saturday",  "Sat";
        7, "Sunday",    "Sun";
    }
}

lexicon M {
    name "Month names";
    fieldname month;
    lang en;
    pseudo     Month,     Mon;
    tokens {
        1,  "January",   "Jan";
        2,  "February",  "Feb";
        3,  "March",     "Mar";
        4,  "April",     "Apr";
        5,  "May";
        6,  "June",      "Jun";
        7,  "July",      "Jul";
        8,  "August",    "Aug";
        9,  "September", "Sep";
        10, "October",   "Oct";
        11, "November",  "Nov";
        12, "December",  "Dec";
    }
}

grammar J {
    fields year month day;
    optional wday;
    lexicons M W;
    alias pseudo { w wday; dd day; mm month; yyyy year; }
    alias unit { d day; m month; y year; }
    format dmy "{day} |{month:M:a} |{year}";
    format wdmy "{wday:W:a} |{day} |{month:M:a} |{year}";
    format out { output "{day::os} |{month:M} |{year}"; }
    format full { output "{wday:W} |{day::os} |{month:M} |{year}"; }
    format "dmy+" "{day} |{month:M} |{year}";
    format "wdmy+" "{wday:W} |{day} |{month:M} |{year}";
    format mdy "{month:M:a} |{day}, |{year}";
    format ymd "{year}|:{month:M:a}|:{day}";
    preferred dmy;
}

scheme SE {
    name "Julian Spanish Era";
    base julian year:38;
    grammar J;
}

write nl;
write record.SE 2360610, record.SE 2360611 nl;
write record.SE:dmy "31 dec 1788", record.SE:dmy "1 jan 1789" nl;
write @text.SE:wdmy( 2360610 ), @text.SE:wdmy( 2360611 ) nl;
write @text.SE:wdmy( {s:SE 1788,12,31} ), @text.SE:wdmy( {s:SE 1789,1,1} ) nl;
write @date.SE:dmy( "31 dec 1788" ), @date.SE:dmy( "1 jan 1789" ) nl;
write @date.SE:dmy( {s:SE 1788,12,31} ), @date.SE:dmy( {s:SE 1789,1,1} ) nl;

/*[OUTPUT]

{s:SE 1788, 12, 31, 1}, {s:SE 1789, 1, 1, 2}
{s:SE 1788, 12, 31, 1}, {s:SE 1789, 1, 1, 2}
Mon 31 Dec 1788, Tue 1 Jan 1789
Mon 31 Dec 1788, Tue 1 Jan 1789
2360610, 2360611
2360610, 2360611

[OUTPUT]*/
