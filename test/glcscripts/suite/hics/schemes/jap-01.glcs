// jap-01.glcs  - Test version of Julian Annunciation Pisa using calculated fields.

lexicon W {
    name "Weekday names";
    fieldname wday;
    lang en;
    pseudo    Weekday,  WDay;
    tokens {
        1, "Monday",    "Mon";
        2, "Tuesday",   "Tue";
        3, "Wednesday", "Wed";
        4, "Thursday",  "Thur";
        5, "Friday",    "Fri";
        6, "Saturday",  "Sat";
        7, "Sunday",    "Sun";
    }
}

lexicon M {
    name "Month names";
    fieldname month;
    lang en;
    pseudo     Month,     Mon;
    tokens {
        1,  "January",   "Jan";
        2,  "February",  "Feb";
        3,  "March",     "Mar";
        4,  "April",     "Apr";
        5,  "May";
        6,  "June",      "Jun";
        7,  "July",      "Jul";
        8,  "August",    "Aug";
        9,  "September", "Sep";
        10, "October",   "Oct";
        11, "November",  "Nov";
        12, "December",  "Dec";
    }
}

grammar JAP {
    fields year month day;
    optional wday;
    calculated cyear;
//    calculate {
//        output {: ,,,@if( month<3 or (month=3 and day<25), year, year+1) };
//        input {: @if( month<3 or (month=3 and day<25), cyear, cyear-1) };
//    }
    rank cyear, month, day;
    lexicons M, W;
    alias pseudo { w wday; dd day; mm month; yyyy cyear; }
    alias unit { d day; m month; y year; }

    // Record: year month day cyear
    function calc_to_def {
        year = @if( month<3 or (month=3 and day<25), cyear, cyear-1);
        result = this;
    }
    function def_to_calc {
        cyear = @if( month<3 or (month=3 and day<25), year, year+1);
        result = this;
    }

    use { calculate def_to_calc; to_text def_to_calc; from_text calc_to_def; }

    format  dmc {
        inout "{day} |{month:M:a} |{cyear}";
        use:in calc_to_def;
}
    format  wdmc {
        inout "{wday:W:a} |{day} |{month:M:a} |{cyear}";
        use:in calc_to_def;
    }
    format  out {
        output "{day::os} |{month:M} |{cyear}";
    }
    preferred dmc;
}

scheme JAP {
    name "Julian Annunciation";
    base julian;
    grammar JAP;
}

write nl;
write @scheme.JAP( 2360328 ), @scheme.JAP( 2360329 ) nl;
write @scheme.JAP:dmc( "24 mar 1750" ), @scheme.JAP:dmc( "25 Mar 1751" ) nl;
write @text.JAP:wdmc( 2360328 ), @text.JAP:wdmc( 2360329 ) nl;
write @text.JAP:wdmc( {s:JAP 1750,3,24} ), @text.JAP:wdmc( {s:JAP 1750,3,25} ) nl;
write @text.JAP:wdmc ({s:JAP ,3,24,1750}@calc_to_def), @text.JAP:wdmc ({s:JAP ,3,25,1751}@calc_to_def) nl;
write @date.JAP:dmc( "24 mar 1750" ), @date.JAP:dmc( "25 Mar 1751" ) nl;
write @date.JAP:dmc( {s:JAP 1750,3,24} ), @date.JAP:dmc( {s:JAP 1750,3,25} ) nl;
write @date.JAP:dmc( {s:JAP ,3,24,1750}@calc_to_def ), @date.JAP:dmc( {s:JAP ,3,25,1751}@calc_to_def ) nl;
write @text.JAP:out( 2432814 ) nl;

/*[OUTPUT]

{s:JAP 1750, 3, 24, 1750, 6}, {s:JAP 1750, 3, 25, 1751, 7}
{s:JAP 1750, 3, 24, 1750, 6}, {s:JAP 1750, 3, 25, 1751, 7}
Sat 24 Mar 1750, Sun 25 Mar 1751
Sat 24 Mar 1750, Sun 25 Mar 1751
Sat 24 Mar 1750, Sun 25 Mar 1751
2360328, 2360329
2360328, 2360329
2360328, 2360329
6th September 1949

[OUTPUT]*/
