// ay-02.glc // Test ay hybrid scheme with alternative grammar.

lexicon M {
    name "Month names";
    fieldname month;
    lang en;
    pseudo     Month,     Mon;
    tokens {
        1,  "January",   "Jan";
        2,  "February",  "Feb";
        3,  "March",     "Mar";
        4,  "April",     "Apr";
        5,  "May";
        6,  "June",      "Jun";
        7,  "July",      "Jul";
        8,  "August",    "Aug";
        9,  "September", "Sep";
        10, "October",   "Oct";
        11, "November",  "Nov";
        12, "December",  "Dec";
    }
}

lexicon JG {
    name "Scheme names";
    fieldname scheme;
    lang en;
    pseudo    Scheme, Sch;
    tokens {
        0, "Julian",     "j";
        1, "Gregorian",  "g";
    }
}

grammar JG {
    fields scheme year month day;
    lexicons M JG;
    alias pseudo {
        s, scheme;
    }
    format  dmy {
        inout "{day} |{month:M:a} |{year}";
        rank year month day;
    }
    format  dmys    "{day} |{month:M:a} |{year} |{scheme:JG*:a}";
    preferred dmy;
}

scheme AY {
    name "Astronomical Year";
    base hybrid {
        fields scheme year month day;
        scheme 0 { base julian; }
        change 2299161;  // g# 15 Oct 1582
        scheme 1 { base gregorian; }
    }
    grammar JG;
}


write nl;
write @record.AY( 2299160 ), @record.AY( 2299161 ) nl;
write @record.AY:dmys( "4 Oct 1582 j" ), @record.AY:dmys( "15 Oct 1582 g" ) nl;
write @record.AY:dmys( "4 Oct 1582" ), @record.AY:dmys( "15 Oct 1582" ) nl;
write @text.AY:dmys( 2299160 ), @text.AY:dmys( 2299161 ) nl;
write @text.:dmys( {s:AY 0,1582,10,4} ), @text.:dmys( {s:AY 1,1582,10,15} ) nl;
write @text.AY:dmy( 2299160 ), @text.AY:dmy( 2299161 ) nl;
write @date.AY:dmys( "4 Oct 1582" ), @date.AY:dmys( "15 Oct 1582" ) nl;
write @date.AY:dmy( "4 Oct 1582" ), @date.AY:dmy( "15 Oct 1582" ) nl;
write @date( {s:AY 0,1582,10,4} ), @date( {s:AY 1,1582,10,15} ) nl;

write @date.AY:dmy( "1582" ), @text.AY:dmy( 2298884..2299238 ) nl;
write @date.AY:dmy( "1500" ), @text.AY:dmy( 2268933..2269298 ) nl;
write @date.AY:dmy( "1800" ), @text.AY:dmy( 2378497..2378861 ) nl;

write @date.AY:dmys( "1582" ), @text.AY:dmys( 2298884..2299238 ) nl;
write @date.AY:dmys( "1500" ), @text.AY:dmys( 2268933..2269298 ) nl;
write @date.AY:dmys( "1800" ), @text.AY:dmys( 2378497..2378861 ) nl;

/*[OUTPUT]

{s:AY 0, 1582, 10, 4}, {s:AY 1, 1582, 10, 15}
{s:AY 0, 1582, 10, 4}, {s:AY 1, 1582, 10, 15}
{s:AY 0, 1582, 10, 4}, {s:AY 1, 1582, 10, 15}
4 Oct 1582 j, 15 Oct 1582 g
4 Oct 1582 j, 15 Oct 1582 g
4 Oct 1582, 15 Oct 1582
2299160, 2299161
2299160, 2299161
2299160, 2299161
2298884..2299238, 1582
2268933..2269298, 1500
2378497..2378861, 1800
2298884..2299238, 1 Jan 1582 j..31 Dec 1582 g
2268933..2269298, 1 Jan 1500 j..31 Dec 1500 j
2378497..2378861, 1 Jan 1800 g..31 Dec 1800 g

[OUTPUT]*/
