/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * Name:        lib/hics/hebrew.glcs
 * Project:     Glich: Extendable Script Language.
 * Purpose:     Hebrew scheme for hics library.
 * Author:      Nick Matthews
 * Website:     https://github.com/nickmat/glich
 * Created:     12th November 2023
 * Copyright:   Copyright (c) 2023..2024, Nick Matthews.
 * Licence:     GNU GPLv3
 *
 *  Glich is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  Glich is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with Glich.  If not, see <http://www.gnu.org/licenses/>.
 *
 * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*/

lexicon hm {
    name "Month names";
    fieldname month;
    lang en;
    pseudo     Month,     Mon;
    tokens {
        1, "Nissan",   "Nsn", "Nisan";
        2, "Iyar",     "Iyr", "Iyyar";
        3, "Sivan",    "Svn";
        4, "Tammuz",   "Tmz", "Tamaz";
        5, "Av",       "Aav";
        6, "Elul",     "Ell";
        7, "Tishrei",  "Tsh", "Tishri";
        8, "Cheshvan", "Csh", "Marheshvan", "Heshvan";
        9, "Kislev",   "Ksl";
        10, "Tevet",   "Tvt";
        11, "Shevat",  "Shv", "Shvat";
        12, "Adar",    "Adr";
        13, "Adar II", "Ads";
    }
}

lexicon hcm {
    name "Complete Month names";
    fieldname cmonth;
    inherit hm;
    tokens {
        14, "Adar I",  "Adf";
    }
}

lexicon hw {
    name "Weekday names";
    fieldname wsday; // Note: Days are numbered from Sunday, unlike "wday".
    lang en;
    pseudo    Weekday,      WDay;
    tokens {
        1, "Yom Rishon",   "Rish"; // Sunday
        2, "Yom Sheni",    "Shen"; // Monday
        3, "Yom Shelishi", "Shel"; // Tuesday
        4, "Yom Revi'i",   "Revi"; // Wednesday
        5, "Yom Hamishi",  "Hami"; // Thursday
        6, "Yom Shishi",   "Shis"; // Friday
        7, "Yom Shabbat",  "Shab"; // Saturday
    }
}

grammar h {
    name "Hebrew";
    fields year month day;
    calculated cmonth;
    optional wsday;
    rank year cmonth day;
    lexicons hm, hcm, hw;
    alias pseudo { w wday; dd day; mm month; yyyy year; }
    alias unit { d day; m month; y year; }

    function calc_cmonth {
        cmonth = @if( month=12 and @leapyear.hebrew(year), 14, month );
        result = this;
    }
    function calc_month {
        month = @if( cmonth=14, 12, cmonth );
        result = this;
    }
    use { calculate calc_cmonth; from:text calc_month; }

    format  dmy_a "{day} |{cmonth:hcm:a} |{year}";
    format  dmy   "{day} |{cmonth:hcm} |{year}";
    format "dmy-" {
        inout "{day} |{month} |{year}";
        rank year month day;
        use:in calc_cmonth;
    }
    format  wdmy  "{wsday:hw} |{day} |{cmonth:hcm} |{year}";
    format  ymd   "{year}| {cmonth:hcm}| {day}";
    format "ymd-" {
        inout "{year}| {month}| {day}";
        rank year month day;
        use:in calc_cmonth;
    }
    format gedcom {
        inout "{day} |{month:hm:a:u} |{year}";
        rank year month day;
        use:in calc_cmonth;
    }
    preferred dmy;
}

scheme h {
    name "Hebrew";
    base hebrew;
    grammar h;
}

// End of lib/hics/hebrew.glcs file
