/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * Name:        lib/hics/chinese.glcs
 * Project:     Glich: Extendable Script Language.
 * Purpose:     Default script for Chinese Calendar Schemes.
 * Author:      Nick Matthews
 * Website:     https://github.com/nickmat/glich
 * Created:     14th July 2024
 * Copyright:   Copyright (c) 2024..2025, Nick Matthews.
 * Licence:     GNU GPLv3
 *
 *  Glich is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  Glich is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with Glich.  If not, see <http://www.gnu.org/licenses/>.
 *
 * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*/


lexicon cstem {
    name "Chinese Stem";
    fieldname stem;
    lang en;
    pseudo   Stem,   Stm;
    tokens {
        1,  "Jiǎ",  "Jia";
        2,  "Yǐ",   "Yi";
        3,  "Bǐng", "Bing";
        4,  "Dīng", "Ding";
        5,  "Wù",   "Wu";
        6,  "Jǐ",   "Ji";
        7,  "Gēng", "Geng";
        8,  "Xīn",  "Xin";
        9,  "Rén",  "Ren";
        10, "Guì",  "Gui";
    };
};

lexicon cbrch {
    name "Chinese Branch";
    fieldname branch;
    lang en;
    pseudo  branch,  brch;
    tokens {
        1,  "Zǐ",   "Zi";
        2,  "Chǒu", "Chou";
        3,  "Yín",  "Yin";
        4,  "Mǎo",  "Mao";
        5,  "Chén", "Chen";
        6,  "Sì",   "Si";
        7,  "Wǔ",   "Wu";
        8,  "Wèi",  "Wei";
        9,  "Shēn", "Shen";
        10, "Yǒu",  "You";
        11, "Xū",   "Xu";
        12, "Hài",  "Hai";
    };
};

lexicon animal {
    name "Chinese Year Animals";
    fieldname branch;
    lang en;
    pseudo  Animal;
    tokens {
        1,  "Rat";
        2,  "Ox";
        3,  "Tiger";
        4,  "Rabbit";
        5,  "Dragon";
        6,  "Snake";
        7,  "Horse";
        8,  "Goat";
        9,  "Monkey";
        10, "Rooster";
        11, "Dog";
        12, "Pig";
    };
};

lexicon lmon {
    name "Leap month";
    fieldname clmonth;
    lang en;
    pseudo     LeapMon,     LM;
    tokens {
        0,  "Month";
        1,  "Leap-month", "Leap";
    };
};

lexicon fs {
    name "First and Second";
    fieldname lmonth;
    lang en;
    pseudo     FirstSecond,     FS;
    tokens {
        0,  "First", "F";
        1,  "Second", "S";
    };
};

grammar c {
    fields year month lmonth day;
    calculated cycle cyear clmonth stem branch;
    // Record: year month lmonth day cycle cyear clmonth stem branch

    lexicons lmon fs cstem cbrch animal;

    alias pseudo { yy year; mm month; lm lmonth; cm clmonth; dd day; cc cycle; cy cyear; ss stem; bb branch; };
    alias unit { d day; m month; lm lmonth; y cyear; c cycle; cm clmonth; s stem; b branch; };

    function calculate {
        cycle = ( (year - 1) div 60 ) + 1;
        cyear = ( (year - 1) mod 60 ) + 1;
        stem = (cyear - 1) mod 10 + 1;
        branch = (cyear - 1) mod 12 + 1;
        clmonth = @if(lmonth=0, null, 1);
        result = this;
    };

    function fixed { // format cymld
        if stem <> null and branch <> null {
            cyear = ((stem - branch) mod 12) * 5 + stem;
        };
        if cycle <> null and cyear <> null {
            year = (cycle - 1) * 60 + cyear;
        };
        clmonth = @if(lmonth=0, null, 1);
        stem = (cyear - 1) mod 10 + 1;
        branch = (cyear - 1) mod 12 + 1;
        result = this;
    }

    function cycle_to_year { // format cylmd
        if stem <> null and branch <> null {
            cyear = ((stem - branch) mod 12) * 5 + stem;
        };
        if cycle <> null and cyear <> null {
            year = (cycle - 1) * 60 + cyear;
        };
        lmonth = @if( clmonth = null, 0, clmonth );
        clmonth = @if(lmonth=0, null, 1);
        stem = (cyear - 1) mod 10 + 1;
        branch = (cyear - 1) mod 12 + 1;
        result = this;
    };

    format ymld "{year}| {month}| {lmonth:fs:a}| {day}";
    format ylmd "{year}| {lmonth:lmon:a}| {month}| {day}";
    // Note, format used in "Calendrical Calculations"
    format cymld {
        inout "{cycle}| {cyear}| {month}| {lmonth:fs:a:l}| {day}";
        rank cycle cyear month lmonth day;
    };
    format cylmd {
        inout "{cycle}| {cyear}| {clmonth:lmon*:a}| {month}| {day}";
        rank cycle cyear month day lmonth;
        use:in cycle_to_year;
    };
    format cylmd_s {
        inout "{cycle}| {stem:cstem}|-{branch:cbrch::l}|, {clmonth:lmon:a}| {month}| {day}";
        rank cycle month day;
        separators ",-";
        use:in cycle_to_year;
    };
    format full {
        output "Cycle: {cycle},| Year: {stem:cstem}-{branch:cbrch::l} ({cyear}),| {lmonth:lmon}:| {month},| Day: {day}";
    };
    format animal {
        output "(Cycle {cycle})| Year of the {branch:animal} {cyear}| {lmonth:lmon}| {month}| Day {day}";
    };
    preferred cymld;
};

scheme c {
  name "Chinese";
  base chinese;
  grammar c;
};

// End of lib/hics/chinese.glcs file
